/* NPC reactions to specific crucial knowledge about Caelar's plans */

/* 1. Hephernaan's own plans */
/* GlobalLT("bd_plot","global",485) //before the portal is opened */

/* 1.1 General reaction to Hephernaan is betraying Caelar: knowledge gained BEFORE the portal scene at Dragonspear basement; PC does NOT know that he is working for a fiend yet. */
/* This will only play with Road to Discovery installed. */
IF
/* activation */
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!See("bdcaelar")
	!See("bdhepher")
	Global("C#RtD_HephernaanBetrayal","GLOBAL",2)
	Global("C#RtD_HephernaanFiend","GLOBAL",0)
	GlobalLT("bd_plot","global",485) //before portal scene
	Global("C#IM_SoD_HephernaanBetrayal","GLOBAL",0) 
THEN 
	RESPONSE #100
		SetGlobal("C#IM_SoD_HephernaanBetrayal","GLOBAL",1)
END
/* initiation */
IF
	InParty("%IMOEN_DV_SOD%")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_SoD_HephernaanBetrayal","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END

/* 1.2 General reaction to the knowledge that Hephernaan is working for a fiend */
/* this dialogue only happens if the knowledge is gained through triggers from Road to Discovery */
/* activation */
IF
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
	GlobalLT("bd_plot","global",485)
	OR(3) //not inside castle if crusaders still hold it
		!AreaCheck("bd4300")
		GlobalLT("bd_plot","GLOBAL",315)
		GlobalGT("bd_plot","GLOBAL",380)
	OR(2) //not inside the crusader camp near entrance 
		!AreaCheck("bd5100")
		!Range("TranBD5000",35) 
	!AreaCheck("bd5200")
	OR(2) //not inside the crusader camp near entrance 
		!AreaCheck("bd5000")
		!Range("TranBD5100",35) 
	Global("C#IM_SoD_HephernaanFiend","GLOBAL",0) 
THEN 
	RESPONSE #100
		SetGlobal("C#IM_SoD_HephernaanFiend","GLOBAL",1)
END
/* initiation */
IF
	InParty("%IMOEN_DV_SOD%")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_SoD_HephernaanFiend","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END


/* 3. Detection: Found treatise "bdbook11": learn about the dormant portal and that godly blood is needed to open it. */
/* activate */
IF
	InParty("%IMOEN_DV_SOD%")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	PartyHasItem("bdbook11")
	Global("C#IM_SoD_Foundbdbook11","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoD_Foundbdbook11","GLOBAL",1)
END



/* 5. Caelar wants to open a portal to Avernus. */

/* 5.1 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND Hephernaan is working for a fiend. */
/* only with Road to Discovery */
/* activation */
IF 
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#RtD_CaelarPlan","GLOBAL",3)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
	GlobalLT("C#RtD_CaelarPlan","GLOBAL",4)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#IM_SoD_PortalConclusion","GLOBAL",0)
	Global("C#SoD_PortalConclusion_1","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoD_PortalConclusion","GLOBAL",1) 
END
/* initiation block see below! */

/* 5.2 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND that there is a dormant rift below Dragonspear Castle. */
/* activation */
IF 
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#RtD_CaelarPlan","GLOBAL",3)
		Global("C#RtD_RoadToDiscovery","GLOBAL",0)
	OR(2)
		GlobalGT("bd_plot","global",169) //after CWC Parley with Caelar
		Global("C#RtD_RoadToDiscovery","GLOBAL",1)
	OR(3)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",1)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",2)
		Global("C#IM_SoD_Foundbdbook11","GLOBAL",1)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#IM_SoD_PortalConclusion","GLOBAL",0)
	Global("C#SoD_PortalConclusion_2","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoD_PortalConclusion","GLOBAL",2) 
END
/* initiation block see below! */

/* 5.3 Conclusion that Caelar wants to open a portal to Avernus: if PC knows that Caelar is planning on marching into Avernus AND that PC's blood is needed to open portal below Dragonspear Castle. */
/* triggers with Road to Discovery or if NPC conlcuded that Caelar needs PC's blood for portal */
/* activation */
IF 
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#RtD_CaelarPlan","GLOBAL",3)
	OR(2)
		GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2)
		Global("C#IM_SoD_PortalPCBloodConclusion","GLOBAL",3)
	GlobalLT("C#RtD_CaelarPlan","GLOBAL",4)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#IM_SoD_PortalConclusion","GLOBAL",0)
	Global("C#SoD_PortalConclusion_3","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoD_PortalConclusion","GLOBAL",3) 
END

/* Initiation for all three conclusion possibilities. Appropriate dialogue will be triggered via variable value set in the activation block. */
IF
	InParty("%IMOEN_DV_SOD%")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("C#IM_SoD_PortalConclusion","GLOBAL",0)
	GlobalLT("C#IM_SoD_PortalConclusion","GLOBAL",4)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END

/* 5.4 General reaction: Caelar wants to open a portal to Avernus. (No reaction to needed blood, PC might not know yet.) BEFORE portal scene at Dragonspear Castle */
/* without Road to Discovery, this will trigger after parley at Dead Man's Pass or if NPC had conclusion himself. */
/* activation */
IF
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	!See([ENEMY])
	See(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",2) //from Road to Discovery
	OR(4)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3) //from Road to Discovery
		Global("C#IM_SoD_PortalConclusion","GLOBAL",4)
  		GlobalGT("bd_plot","global",390) //after meeting with Caelar on Dead Man's Pass
		Global("C#RtD_RoadToDiscovery","GLOBAL",1) //check for Road to Discovery
	OR(3)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3) //from Road to Discovery
		Global("C#IM_SoD_PortalConclusion","GLOBAL",4)
		Global("C#RtD_RoadToDiscovery","GLOBAL",0) //broaden trigger if Road to Discovery is not installed
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#IM_SoD_PortalAvernus","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoD_PortalAvernus","GLOBAL",1)
END
/* initiation */
IF
	InParty("%IMOEN_DV_SOD%")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_SoD_PortalAvernus","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END

/* 7. Caelar needs the PC's blood to open the portal. */

/* 7.1 Conclusion that Caelar needs the PC's blood to open the portal: if Caelar needs Bhaalblood to open portal AND PC knows that she is no Bhaalspawn herself. */
/* this can trigger without Road to Discovery if treatise was found and NPC concluded that Caelar wants to open a portal. */
IF 
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",2) 
		Global("C#IM_SoD_Foundbdbook11","GLOBAL",1)
	OR(2)
		GlobalGT("C#RtD_CaelarBhaalChild","GLOBAL",1)
		GlobalGT("bd_mdd018","global",0)
	OR(2)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3)
		Global("C#RtD_RoadToDiscovery","GLOBAL",0)
	OR(3)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3)
		Global("C#IM_SoD_PortalConclusion","GLOBAL",4)
		Global("C#RtD_RoadToDiscovery","GLOBAL",1)
	GlobalLT("C#RtD_WantBhaalBlood","GLOBAL",2)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",3)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#IM_SoD_PortalPCBloodConclusion","GLOBAL",0)
	Global("C#SoD_PortalPCBloodConclusion_1","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoD_PortalPCBloodConclusion","GLOBAL",1)
END
/* initiation */
IF
	InParty("%IMOEN_DV_SOD%")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_SoD_PortalPCBloodConclusion","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END

/* 7.2 Conclusion that Caelar needs the PC's blood to open the portal: if Caelar needs Bhaalblood AND wanted to kidnap the PC. */
/* will only trigger with Road to Discovery variables. */
/* activation */
IF 
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#RtD_KnowsPortalBlood","GLOBAL",2) 
		Global("C#IM_SoD_Foundbdbook11","GLOBAL",1)
	OR(3)
		Global("C#RtD_WantBhaalBlood","GLOBAL",1)
		Global("C#RtD_CaelarKidnap","GLOBAL",1)
		GlobalGT("C#RtD_CaelarWantsPC","GLOBAL",1)
	OR(2)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3)
		Global("C#IM_SoD_PortalConclusion","GLOBAL",4)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",3)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#IM_SoD_PortalPCBloodConclusion","GLOBAL",0)
	Global("C#SoD_PortalPCBloodConclusion_2","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoD_PortalPCBloodConclusion","GLOBAL",2)
END
/* initiation */
IF
	InParty("%IMOEN_DV_SOD%")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_SoD_PortalPCBloodConclusion","GLOBAL",2)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END

/* 7.3 + 7.4 + 7.5 General reaction to the knowledge that Caelar needs the PC's blood to open the portal. */
/* activation */
IF
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	!See([ENEMY])
	See(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	OR(3)
		GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2) //from Road to Discovery
		Global("C#IM_SoD_PortalPCBloodConclusion","GLOBAL",3)
		Global("C#IM_SoDPortalScene","GLOBAL",1)
	Global("C#IM_SoD_PortalPCBlood","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoD_PortalPCBlood","GLOBAL",1)
END
/* initiation */
IF
	InParty("%IMOEN_DV_SOD%")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_SoD_PortalPCBlood","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END




/* Incident at Boareskyr Bridge. */

/* Quick reaction line: will only show if The Boareskyr Bridge Scene mod is not installed / didn't run. Uses the same line! -> crossmod with TBBS is in the tp2. */
/* activation */
/* no timer here! */
IF
	InParty("%IMOEN_DV_SOD%")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	!See([ENEMY])
	CombatCounter(0)
	GlobalGT("bd_plot","global",294) //after it happened
	GlobalLT("chapter","global",10) // not if game went on
	AreaCheck("bd2000")
	!InMyArea("bdireni")
	!InMyArea("bdfist24")
	Global("C#IM_SoD_c#stff24_10","GLOBAL",0) // no interjection from tbbs
	Global("C#IM_SoDBoareskyrBridgeScene","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoDBoareskyrBridgeScene","GLOBAL",1)
END

/* initiate */
IF
	InParty("%IMOEN_DV_SOD%")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	!See([ENEMY])
	CombatCounter(0)
	Global("C#IM_SoDBoareskyrBridgeScene","GLOBAL",1)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%) //we set timer here so there is a space to whichever next dialogue
		StartDialogNoSet(Player1)
END

/* General dialogue after bridge incident. Will run after timer. (True also in case The Boareskyr Bridge mod is installed and the above quick reaction line didn't run.) This follow-up dialogue triggers after timer is run. */
/* activation */
IF
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	!See([ENEMY])
	CombatCounter(0)
	GlobalGT("bd_plot","global",294) //after it happened
	OR(2)
		Global("C#IM_SoDBoareskyrBridgeScene","GLOBAL",2) //quick reaction line run
		Global("C#IM_SoD_c#stff24_10","GLOBAL",1) // interjection from tbbs run
	GlobalLT("C#IM_SoDBoareskyrBridgeScene","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoDBoareskyrBridgeScene","GLOBAL",3)
END
/* initiate */
IF
	InParty("%IMOEN_DV_SOD%")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	!See([ENEMY])
	CombatCounter(0)
	Global("C#IM_SoDBoareskyrBridgeScene","GLOBAL",3)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END

/* Caelar is no child of Bhaal. */

/* Conclusion that Caelar is no child of Bhaal: after PC reasons / concludes / speculates that Caelar / Hephernaan want PC's blood for [open portal to Avernus] AND hearing rumors that she is a child of Bhaal (or the portal would already be open / she wouldn't go for the PC.) */
/* will only trigger with Road to Discovery because there is no other ingame trigger to check whether the PC heard the rumor. */
/* activation */
IF
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2)
	Global("C#RtD_CaelarBhaalChild","GLOBAL",1)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#IM_SoD_CaelarNoBhaalChild","GLOBAL",0) 
	Global("C#SoD_CaelarNoBhaalChild","GLOBAL",0) // no other NPC offered this conclusion yet. Leave this in!
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoD_CaelarNoBhaalChild","GLOBAL",1) 
END
/* initiation */
IF
	InParty("%IMOEN_DV_SOD%")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_SoD_CaelarNoBhaalChild","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END

/* Reaction to the knowledge that Caelar is no Bhaalchild. */
/* activation */
/* without Road to Discovery, this will only trigger if Caelar was asked about it in the Coastway Crossing parley. */
IF
	InParty("%IMOEN_DV_SOD%")
	!RealGlobalTimerNotExpired("C#IM_DialogueTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(3)
  		GlobalGT("bd_mdd018","global",0) //Caelar told she is no child of Bhaal
		GlobalGT("C#RtD_CaelarBhaalChild","GLOBAL",1) //from Road to Discovery
		Global("C#IM_SoD_CaelarNoBhaalChild","GLOBAL",2)
	AreaCheck("bd3000") //after Boareskyr bridge scene, and inside camp
	GlobalLT("C#IM_SoD_CaelarNoBhaalChild","GLOBAL",3) 
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoD_CaelarNoBhaalChild","GLOBAL",3) 
END
/* initiation */
IF
	InParty("%IMOEN_DV_SOD%")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_SoD_CaelarNoBhaalChild","GLOBAL",3)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END

