/* imoen_bd4300_commenting.baf */
/* 1 Hephernaan discovered the group inside the castle */
IF
	Global("bd_plot","global",370)
	!GlobalTimerNotExpired("bd_mdd905a_ot_timer","bd4300")  
	Global("C#Imoen_bd_ot","bd4300",0)  
	IfValidForPartyDialog("%IMOEN_DV_SOD%")  
THEN
	RESPONSE #100
		SetGlobalTimer("bd_mdd905a_ot_timer","bd4300",THREE_MINUTES)  
		SetGlobal("C#Imoen_bd_ot","bd4300",1)  
		DisplayStringHead("%IMOEN_DV_SOD%",@5520)  
		Continue()  
END

/* 2 Portal is opened after Hephernaan's scheme */
IF
	AreaCheck("BD4300")
	Global("bd_plot","global",495)
	Global("C#Imoen_SoDPortalComment","bd4300",0) 
	IfValidForPartyDialog("%IMOEN_DV_SOD%")  
THEN
	RESPONSE #100
		SetGlobal("C#Imoen_SoDPortalComment","bd4300",1)
		DisplayStringHead("%IMOEN_DV_SOD%",@5521)  
		SmallWait(8)
		Continue()
END


/* 3 PC killed the crusaders at the portal */
IF
	Global("C#Imoen_bd_MDD892a_ot","bd4300",0)  
	Global("bd_poison_dsc_supplies","global",0)
	Dead("bdbelben")  // Belben
	CombatCounter(0)
	IfValidForPartyDialog("%IMOEN_DV_SOD%")  
THEN
	RESPONSE #100
		SetGlobal("C#Imoen_bd_MDD892a_ot","bd4300",1)  
		SetGlobalTimer("bd_MDD892a_ot_timer","bd4300",ONE_MINUTE) //timer will be checked for Corwin 
		DisplayStringHead("%IMOEN_DV_SOD%",@5522)   
		Continue() 
END

/* 4 Comment after first wave of demons if PC lingers */
IF
	GlobalLT("bd_plot","global",499)
	Global("bd_mdd1290d_ot","bd4300",1)  
	Global("C#Imoen_bd_mdd1290d_ot","bd4300",0)  
	IfValidForPartyDialog("%IMOEN_DV_SOD%")  
THEN
	RESPONSE #100
		DisplayStringHead("%IMOEN_DV_SOD%",@5523)  
		SetGlobal("C#Imoen_bd_mdd1290d_ot","bd4300",1)   
		SmallWait(8) 
		Continue()
END

/* 5 Comment after return from Avernus: portal is closed */
IF
	Global("bd_plot","global",586)
	!GlobalTimerNotExpired("bd_mdd1341a_ot_timer","bd4300")  
	Global("C#Imoen_bd_ot2","bd4300",0)  
	IfValidForPartyDialog("%IMOEN_DV_SOD%")  
THEN
	RESPONSE #100
		SetGlobalTimer("bd_mdd1341a_ot_timer","bd4300",2)  // Dragonspear Castle Basement
		SetGlobal("C#Imoen_bd_ot2","bd4300",1)  
		DisplayStringHead("%IMOEN_DV_SOD%",@5524)  
		Continue()
END