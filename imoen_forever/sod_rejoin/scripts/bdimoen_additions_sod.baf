/* set dialogue timers after joining in bd1000 */
IF
	InParty(Myself)
	CombatCounter(0)
	!See([ENEMY])
	GlobalGT("C#IM_ImoenRejoinsbd1000","GLOBAL",0)
	Global("C#IM_DialogueTimerSoDSet","LOCALS",0)
THEN  
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		SetGlobal("C#IM_DialogueSoD","GLOBAL",1)
		SetGlobal("C#IM_DialogueTimerSoDSet","LOCALS",1)
END



/* Reaction to the destroyed Bridge at Coastway Crossing */
IF
	GlobalTimerNotExpired("bd_caelar_timer","bd1000")
	AreaCheck("bd1000")
	IsValidForPartyDialog(Myself)
	Detect(Player1)	
	Global("C#IM_SoDbridge","bd1000",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoDbridge","bd1000",1)
		DisplayStringHead(Myself,@5050)
END

/* PC killed crusaders with the spikes */
IF
	InParty(Myself)
	IsValidForPartyDialog(Myself)
	See(Player1)
	AreaCheck("bd7230")
	Global("C#IM_UsedSpikes","bd7230",1) //this variable is set by patching bdlever2.BCS via tp2
	CombatCounter(0)
	!GlobalTimerNotExpired("C#SoD_spiketrap_timer","MYAREA") //If you use exactly this timer (with my prefix), then all NPC mods could be spaced so they do not talk at the same time. Otherwise you don't need a timer here.
THEN
	RESPONSE #100
		SetGlobal("C#IM_UsedSpikes","bd7230",2)
		SetGlobalTimer("C#SoD_spiketrap_timer","MYAREA",ONE_ROUND) //use this with my prefix for compatibility
		DisplayStringHead(Myself,@5051)
END

/* check: PC had nightmare without Imoen being around */

IF
	!InParty(Myself)
	!Range(Player1,30)  //not in party, not nearby
	Global("bd_ddd","global",2) //second nightmare happened
	Global("C#IM_SoDNightmare","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoDNightmare","GLOBAL",2) 
END


/* PC had first nightmare: Imoen - slayer */
IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	Global("bd_ddd","global",2) //first nightmare happened
	CombatCounter(0)
	Global("C#IM_SoDNightmare","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoDNightmare","GLOBAL",1) 
END


/* triggering. */
IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("C#IM_SoDNightmare","GLOBAL",1) 
	CombatCounter(0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogueNoSet(Player1)
END

/* Imoen reacts to Thrix's game */

/* PC offered Imoen's soul */
IF
	IsValidForPartyDialogue(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#Imoen_SoDThrix","GLOBAL",2)
		Global("C#Imoen_SoDThrix","GLOBAL",3)
	GlobalLT("bd_plot","global",550)
	OR(2)
		Global("bd_thrix_plot","global",20)
		Dead("bdthrix")  // Thrix
THEN
	RESPONSE #100
		IncrementGlobal("C#Imoen_SoDThrix","GLOBAL",3)
END

/* PC offered someone else's soul */
IF
	IsValidForPartyDialogue(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	!Global("C#Imoen_SoDThrix","GLOBAL",2)
	!Global("C#Imoen_SoDThrix","GLOBAL",3)
	GlobalLT("C#Imoen_SoDThrixReaction","GLOBAL",7)
	Global("bd_thrix_sacrifice_companion","global",1)
	!Global("bd_thrix_sacrifice_self","global",1)
	GlobalLT("bd_plot","global",550)
	OR(2)
		Global("bd_thrix_plot","global",20)
		Dead("bdthrix")  // Thrix
THEN
	RESPONSE #100
		SetGlobal("C#Imoen_SoDThrixReaction","GLOBAL",7)
END

/* PC offered own soul after other NPC was chosen */

IF
	IsValidForPartyDialogue(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	!Global("C#Imoen_SoDThrix","GLOBAL",1)
	!Global("C#Imoen_SoDThrix","GLOBAL",2)
	!Global("C#Imoen_SoDThrix","GLOBAL",3)
	GlobalLT("C#Imoen_SoDThrixReaction","GLOBAL",8)
	Global("bd_thrix_sacrifice_self","global",1)
	GlobalLT("bd_plot","global",550)
	OR(2)
		Global("bd_thrix_plot","global",20)
		Dead("bdthrix")  // Thrix
THEN
	RESPONSE #100
		SetGlobal("C#Imoen_SoDThrixReaction","GLOBAL",9)
END

/* PC offered own soul after Imoen was chosen */

IF
	IsValidForPartyDialogue(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#Imoen_SoDThrix","GLOBAL",1)
	Global("bd_thrix_sacrifice_self","global",1)
	GlobalLT("bd_plot","global",550)
	OR(2)
		Global("bd_thrix_plot","global",20)
		Dead("bdthrix")  // Thrix
THEN
	RESPONSE #100
		SetGlobal("C#Imoen_SoDThrix","GLOBAL",8)
END

/* trigger */
IF
	IsValidForPartyDialogue(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	OR(5)
		Global("C#Imoen_SoDThrixReaction","GLOBAL",9)
		Global("C#Imoen_SoDThrix","GLOBAL",8)
		Global("C#Imoen_SoDThrixReaction","GLOBAL",7)
		Global("C#Imoen_SoDThrix","GLOBAL",6)
		Global("C#Imoen_SoDThrix","GLOBAL",5)		
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END

/* Follow up to Thrix: why would my soul be special? */

IF
	IsValidForPartyDialogue(Myself)
	RealGlobalTimerExpired("C#IM_DialogueTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	GlobalGT("C#Imoen_SoDThrix","GLOBAL",0)
//	!AreaCheck("bd4500")
//	!AreaCheck("bd4700")
	!See("bdaun")
	!See("bddelanc")
	Global("C#Imoen_SoDThrixSoul","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Imoen_SoDThrixSoul","GLOBAL",1)
END

/* trigger */
IF
	IsValidForPartyDialogue(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#Imoen_SoDThrixSoul","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END

/* reaction to item: */
/* bdamul02 "Archer's Eyes" */
IF
	IsValidForPartyDialogue(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	PartyHasItemIdentified("bdamul02")
	Global("C#Imoen_SoDbdamul02","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Imoen_SoDbdamul02","GLOBAL",1)
END

/* trigger */
IF
	IsValidForPartyDialogue(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#Imoen_SoDbdamul02","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogNoSet(Player1)
END

/* timered talks */
/* after meeting Caelar at the bridge */

/* activate */
IF
    InParty(Myself)
    RealGlobalTimerExpired("C#IM_DialogueTimer","GLOBAL")
	!StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
	GlobalGT("bd_plot","global",169)
	!See("bdcaelar")
	Global("C#IM_SoDCaelarBridge","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_SoDCaelarBridge","GLOBAL",1)
END

/* initiate */
IF 
  Global("C#IM_SoDCaelarBridge","GLOBAL",1)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogueNoSet(Player1)
END

/* Friendship dialogues */
/* activate */
IF 
    RealGlobalTimerExpired("C#IM_DialogueTimer","GLOBAL")
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
    OR(2)
	Global("C#IM_DialogueSoD","GLOBAL",1)
	Global("C#IM_DialogueSoD","GLOBAL",3)
THEN
	RESPONSE #100
		IncrementGlobal("C#IM_DialogueSoD","GLOBAL",1)
END

/* initiate */
IF 
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
    OR(2)
	Global("C#IM_DialogueSoD","GLOBAL",2)
	Global("C#IM_DialogueSoD","GLOBAL",4)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",%Dialog_Timer_SoD%)
		StartDialogueNoSet(Player1)
END




/* adjust XP after joining once */
IF
	Global("BD_JOINXP","LOCALS",1)
	Global("BD_SAFEHOUSE_DONE","GLOBAL",1)
	Global("BDSODXP","LOCALS",0)
	InParty(Myself)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BDSODXP","LOCALS",1)
		SetGlobal("BD_JOINXP","LOCALS",0)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,249999)
	XPLT(Myself,250000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,250000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,199999)
	XPLT(Myself,200000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,200000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,160999)
	XPLT(Myself,161000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,161000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,134999)
	XPLT(Player1,161000)
	XPLT(Myself,135000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,135000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,109999)
	XPLT(Player1,135000)
	XPLT(Myself,110000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,110000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPGT(Player1,89999)
	XPLT(Player1,110000)
	XPLT(Myself,90000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		ChangeStat(Myself,XP,90000,SET)
		SetInterrupt(TRUE)
END

IF
	Global("BD_JOINXP","LOCALS",0)
	InParty(Myself)
	XPLT(Player1,90000)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("BD_JOINXP","LOCALS",1)
		SetInterrupt(TRUE)
END

