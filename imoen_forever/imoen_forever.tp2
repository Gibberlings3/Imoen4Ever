/*
	***						  ***
	*** This whole mod is a spoiler to the BGII story ***	
	***						  ***

*/

BACKUP ~imoen_forever/backup~
AUTHOR ~Please post at G3 or Kerzenburgforum, refer to readme.~  


VERSION ~v8pre~  

README ~imoen_forever/readme.imoen_forever.%LANGUAGE%.txt~ ~imoen_forever/readme.imoen_forever.english.txt~


AUTO_TRA ~imoen_forever/tra/autotra/%s~

ALWAYS

/* check for a ready SoD */

ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN
  FAIL @1002 /* ~Modmerge or Argent's DLC Merger is required before mods can be installed on this game.~ */
END



  ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SET bg2_chapter = 12
  END ELSE BEGIN
    OUTER_SET bg2_chapter = 0
  END
  OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
    OUTER_SET bg2_chapter = bg2_chapter + 1
    OUTER_SPRINT name_source ~bg2_chapter_%i%~
    OUTER_SET EVAL ~%name_source%~ = bg2_chapter
  END

  INCLUDE ~imoen_forever/lib/extra_regexp_vars.tph~
  INCLUDE ~imoen_forever/lib/get_respone_sstrrefs.tph~

  ACTION_IF GAME_IS ~bgt~ THEN BEGIN
    INCLUDE ~imoen_forever/lib/g3_bgt_cpmvars.tpa~ 
  END

  ACTION_IF GAME_IS ~bgee~ THEN BEGIN
    INCLUDE ~imoen_forever/lib/g3_bgee_cpmvars.tpa~
  END

  ACTION_IF GAME_IS ~eet~ THEN BEGIN
    INCLUDE ~EET/other/cpmvars/eet_cpmvars.tpa~
  END

OUTER_SPRINT newline ~%WNL%%LNL%%MNL%%TAB% ~

ACTION_IF GAME_IS ~bgee~ THEN BEGIN
  OUTER_SPRINT ~IMOEN_DV_SOD~ ~IMOEN~
  OUTER_SPRINT ~6377~ ~6377~
  OUTER_SPRINT ~11035~ ~11035~
  OUTER_SPRINT ~16413~ ~16413~
  OUTER_SPRINT ~16414~ ~16414~
  OUTER_SPRINT ~16415~ ~16415~
  OUTER_SPRINT ~35302~ ~35302~
  OUTER_SPRINT ~35304~ ~35304~
  OUTER_SPRINT ~35323~ ~35323~
  OUTER_SPRINT ~39771~ ~39771~
  OUTER_SPRINT ~35320~ ~35320~
  OUTER_SPRINT ~35321~ ~35321~
  OUTER_SPRINT ~35322~ ~35322~
  OUTER_SPRINT ~35327~ ~35327~
  OUTER_SPRINT ~35344~ ~35344~
  OUTER_SPRINT ~35345~ ~35345~
  OUTER_SPRINT ~35347~ ~35347~
  OUTER_SPRINT ~35350~ ~35350~
  OUTER_SPRINT ~38886~ ~38886~
  OUTER_SPRINT ~39753~ ~39753~
  OUTER_SPRINT ~39771~ ~39771~
  OUTER_SPRINT ~52921~ ~52921~
  OUTER_SPRINT ~55721~ ~55721~
  OUTER_SPRINT ~55722~ ~55722~
  OUTER_SPRINT ~55723~ ~55723~
  OUTER_SPRINT ~55725~ ~55725~
  OUTER_SPRINT ~55726~ ~55726~
  OUTER_SPRINT ~55732~ ~55732~
  OUTER_SPRINT ~55743~ ~55743~
  OUTER_SPRINT ~55744~ ~55744~
  OUTER_SPRINT ~55745~ ~55745~
  OUTER_SPRINT ~55746~ ~55746~
  OUTER_SPRINT ~56051~ ~56051~
  OUTER_SPRINT ~57089~ ~57089~
  OUTER_SPRINT ~63916~ ~63916~
  OUTER_SPRINT ~63943~ ~63943~
  OUTER_SPRINT ~63944~ ~63944~
  OUTER_SPRINT ~63958~ ~63958~
  OUTER_SPRINT ~63960~ ~63960~
  OUTER_SPRINT ~63974~ ~63974~
  OUTER_SPRINT ~63975~ ~63975~
  OUTER_SPRINT ~63976~ ~63976~
  OUTER_SPRINT ~63977~ ~63977~
  OUTER_SPRINT ~63978~ ~63978~
  OUTER_SPRINT ~64518~ ~64518~
  OUTER_SPRINT ~64521~ ~64521~
  OUTER_SPRINT ~64522~ ~64522~
  OUTER_SPRINT ~64524~ ~64524~
  OUTER_SPRINT ~64525~ ~64525~
  OUTER_SPRINT ~65051~ ~65051~
  OUTER_SPRINT ~66315~ ~66315~
  OUTER_SPRINT ~66358~ ~66358~
  OUTER_SPRINT ~66750~ ~66750~
  OUTER_SPRINT ~66751~ ~66751~
  OUTER_SPRINT ~69021~ ~69021~
  OUTER_SPRINT ~69732~ ~69732~
  OUTER_SPRINT ~69736~ ~69736~
  OUTER_SPRINT ~70436~ ~70436~
END

ACTION_IF GAME_IS ~eet~ THEN BEGIN
  OUTER_SPRINT ~IMOEN_DV_SOD~ ~IMOEN2~
  OUTER_SPRINT ~6377~ ~206377~
  OUTER_SPRINT ~11035~ ~211035~
  OUTER_SPRINT ~16413~ ~216413~
  OUTER_SPRINT ~16414~ ~216414~
  OUTER_SPRINT ~16415~ ~216415~
  OUTER_SPRINT ~35302~ ~235302~
  OUTER_SPRINT ~35304~ ~235304~
  OUTER_SPRINT ~35323~ ~235323~
  OUTER_SPRINT ~38886~ ~238886~
  OUTER_SPRINT ~39771~ ~239771~
  OUTER_SPRINT ~35320~ ~235320~
  OUTER_SPRINT ~35321~ ~235321~
  OUTER_SPRINT ~35322~ ~235322~
  OUTER_SPRINT ~35327~ ~235327~
  OUTER_SPRINT ~35344~ ~235344~
  OUTER_SPRINT ~35345~ ~235345~
  OUTER_SPRINT ~35347~ ~235347~
  OUTER_SPRINT ~35350~ ~235350~
  OUTER_SPRINT ~39753~ ~239753~
  OUTER_SPRINT ~39771~ ~239771~
  OUTER_SPRINT ~52921~ ~252921~
  OUTER_SPRINT ~55721~ ~255721~
  OUTER_SPRINT ~55722~ ~255722~
  OUTER_SPRINT ~55723~ ~255723~
  OUTER_SPRINT ~55725~ ~255725~
  OUTER_SPRINT ~55726~ ~255726~
  OUTER_SPRINT ~55732~ ~255732~
  OUTER_SPRINT ~55743~ ~255743~
  OUTER_SPRINT ~55744~ ~255744~
  OUTER_SPRINT ~55745~ ~255745~
  OUTER_SPRINT ~55746~ ~255746~
  OUTER_SPRINT ~56051~ ~256051~
  OUTER_SPRINT ~57089~ ~257089~
  OUTER_SPRINT ~63916~ ~263916~
  OUTER_SPRINT ~63943~ ~263943~
  OUTER_SPRINT ~63944~ ~263944~
  OUTER_SPRINT ~63958~ ~263958~
  OUTER_SPRINT ~63960~ ~263960~
  OUTER_SPRINT ~63974~ ~263974~
  OUTER_SPRINT ~63975~ ~263975~
  OUTER_SPRINT ~63976~ ~263976~
  OUTER_SPRINT ~63977~ ~263977~
  OUTER_SPRINT ~63978~ ~263978~
  OUTER_SPRINT ~64518~ ~264518~
  OUTER_SPRINT ~64521~ ~264521~
  OUTER_SPRINT ~64522~ ~264522~
  OUTER_SPRINT ~64524~ ~264524~
  OUTER_SPRINT ~64525~ ~264525~
  OUTER_SPRINT ~65051~ ~265051~
  OUTER_SPRINT ~66315~ ~266315~
  OUTER_SPRINT ~66358~ ~266358~
  OUTER_SPRINT ~66750~ ~266750~
  OUTER_SPRINT ~66751~ ~266751~
  OUTER_SPRINT ~69021~ ~269021~
  OUTER_SPRINT ~69732~ ~269732~
  OUTER_SPRINT ~69736~ ~269736~
  OUTER_SPRINT ~70436~ ~270436~
END


//-----Do only once for whole mod-------
ACTION_IF !FILE_EXISTS ~imoen_forever/Install/imoen_foreverinstall.mrk~ BEGIN


//CamDawgs CD_STATE_NOTVALID 
    APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~


    /* BGII:EE, EET - formatted texts etc. */

    /* Copies tra files into the autotra-folder (to leave the originals untouched) */
    DEFINE_ACTION_FUNCTION autotra_workaround BEGIN
      COPY ~imoen_forever/tra/english~  ~imoen_forever/tra/autotra/%LANGUAGE%~
      COPY ~imoen_forever/tra/%LANGUAGE%~  ~imoen_forever/tra/autotra/%LANGUAGE%~
    END

    LAF autotra_workaround END



    ACTION_DEFINE_ARRAY fl#noconvert BEGIN setup END

    ACTION_DEFINE_ARRAY fl#reload BEGIN game END

      LAF HANDLE_CHARSETS
        INT_VAR
          infer_charsets = 1
        STR_VAR
          tra_path = EVAL ~imoen_forever/Tra/autotra~
          noconvert_array = fl#noconvert
          reload_array = fl#reload
      END

/* SoD bdbanter.2da */
ACTION_IF (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~) THEN BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~BDBANTER.2DA~) BEGIN
	APPEND ~bdbanter.2da~
		~Imoen 	BDIMOENB~
	UNLESS ~Imoen~
        
  END ELSE BEGIN 

<<<<<<<< .../BDBANTER.2DA
2DA      V1.0
NONE
         FILE
KIVAN    BDKIVANB
ALORA    BDALORAB
MINSC    BDMINSCB
DYNAHEIR BDDYNAHB
YESLICK  BDYESLIB
CORAN    BDCORANB
AJANTIS  BDAJANTB
KHALID   BDKHALIB
JAHEIRA  BDJAHEIB
GARRICK  BDGARRIB
SAFANA   BDSAFANB
FALDORN  BDFALDOB
BRANWEN  BDBRANWB
QUAYLE   BDQUAYLB
XAN      BDXANB
SKIE     BDSKIEB
ELDOTH   BDELDOTB
XZAR     BDXZARB
MONTARON BDMONTAB
TIAX     BDTIAXB
KAGAIN   BDKAGAIB
SHARTEEL BDSHARTB
EDWIN    BDEDWINB
VICONIA  BDVICONB
IMOEN    BDIMOENB
NEERA    BDNEERAB
DORN     BDDORNB
RASAAD   BDRASAAB
BAELOTH  BDBAELOB
VOGHILN  BDVOGHIB
MKHIIN   BDMKHIIB
CORWIN   BDCORWIB
GLINT    BDGLINTB
>>>>>>>>
    COPY ~.../BDBANTER.2DA~ ~override~ 
  END
END //SoD

// ----End (Do only once for whole mod)----
  COPY ~imoen_forever/Install/component.xx~ ~imoen_forever/Install/imoen_foreverinstall.mrk~
  END // imoen_foreverinstall.mrk

  LOAD_TRA ~imoen_forever/tra/autotra/english/game.tra~
  LOAD_TRA ~imoen_forever/tra/autotra/%LANGUAGE%/game.tra~

END //ALWAYS




LANGUAGE ~English~
         ~english~   
         ~imoen_forever/tra/english/setup.tra~
         ~imoen_forever/tra/english/game.tra~

LANGUAGE ~Deutsch~
         ~german~   
         ~imoen_forever/tra/german/setup.tra~
         ~imoen_forever/tra/german/game.tra~

LANGUAGE ~Polski~
         ~polish~   
         ~imoen_forever/tra/polish/setup.tra~
         ~imoen_forever/tra/polish/game.tra~

LANGUAGE ~Russian~
         ~russian~   
         ~imoen_forever/tra/english/setup.tra~
         ~imoen_forever/tra/russian/game.tra~


////////////////////////////////////////////////////////////////////////
//////
////// Imoen 4 Ever in BGII: Imoen returns at the beginning of chapter 2
//////
////////////////////////////////////////////////////////////////////////

BEGIN @9928 DESIGNATED 0 //GROUP @9921
REQUIRE_PREDICATE GAME_IS ~bg2ee eet bgt tob~ @9905 
REQUIRE_PREDICATE ((GAME_IS ~bg2ee eet~) OR (MOD_IS_INSTALLED ~setup-bg2fixpack.TP2~ "0")) @9901 
REQUIRE_PREDICATE NOT (MOD_IS_INSTALLED ~setup-ImoenIsStone.TP2~ "0") @9902 
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930



ADD_JOURNAL @1000



/* We need a lower level imoen cre for BGII(:EE) & BGT */

ACTION_IF GAME_IS ~tob bg2ee~ THEN BEGIN
  COPY_EXISTING ~imoen10.cre~ ~override/c#im_010.cre~
    WRITE_ASCII 0x280 ~imoen2~ #32 // script name / DV
    WRITE_BYTE  0x240  5 // break
END

/* give other Imoen NPC cres a different DV so they can be removed (I'm sure there is a more elegant way of doing this) */

COPY_EXISTING ~imoen211.cre~ ~override~
	 ~imoen213.cre~ ~override~
    WRITE_ASCII 0x280 ~imoen_notthistime~ #32 // script name / DV
    WRITE_ASCII 0x248 ~~ #8 // OVERRIDE

/* Coweld Wizard for abduction cutscene */

COPY_EXISTING ~cowled01.cre~ ~Override/c#imcowl.cre~
    WRITE_ASCII 0x280 ~c#imcowl~ #32 // script name / DV
    WRITE_ASCII 0x248 ~c#imcowl~ #8 // OVERRIDE
    WRITE_ASCII 0x250 ~~ #8 // CLASS
    WRITE_ASCII 0x258 ~~ #8 // RACE
    WRITE_ASCII 0x260 ~~ #8 // GENERAL
    WRITE_ASCII 0x268 ~~ #8 // DEFAULT
    WRITE_ASCII 0x2cc ~c#imcowl~ #8 // dlg


/* erase references to kidnapped Imoen pre-Brynnlaw from the game */

COMPILE EVALUATE_BUFFER ~imoen_forever/dialogue/npc.d~

ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~imoen_forever/dialogue/npc_ee.d~
END

COMPILE EVALUATE_BUFFER ~imoen_forever/dialogue/general.d~

/* disable all instances where the player asks for kidnapped Imoen, but do it non-destructively. Thank you argent77 for the code! */
INCLUDE ~imoen_forever/tpa/add_trans_trigger_npc.tpa~
INCLUDE ~imoen_forever/tpa/add_trans_trigger_general.tpa~



INCLUDE ~imoen_forever/tpa/string_set.tpa~

ACTION_IF GAME_IS ~bg2ee eet~ THEN BEGIN
  INCLUDE ~imoen_forever/tpa/string_set_ee.tpa~
END


/* Imoen will rejoin in the slums after Gaelan / Brus */

INCLUDE ~imoen_forever/tpa/imoen_ar0400-baf.tpa~
EXTEND_BOTTOM ~ar0400.bcs~ ~...inlined/imoen_ar0400.baf~ EVALUATE_BUFFER 


/* "C#IMMET" Imoen's join dialogue in slums */

COMPILE EVALUATE_BUFFER ~imoen_forever/dialogue/c#immet.d~

/* Imoen needs other kickout dialogue before Brynnlaw */

COMPILE EVALUATE_BUFFER ~imoen_forever/dialogue/c#im_imoen2p.d~


/* put Imoen inside Spellhold. - preparation of "imoen2" is also done in c#imcut1.baf */

INCLUDE ~imoen_forever/tpa/imoen_ar1512-baf.tpa~
EXTEND_TOP ~ar1512.bcs~ ~...inlined/inlined_ar1512.baf~ EVALUATE_BUFFER

/* erase journal entry */
EXTEND_BOTTOM ~ar1512.bcs~ ~imoen_forever/baf/imoen_ar1512.baf~ EVALUATE_BUFFER




/* area patches: give Brynnlaw detection trigger (AR1600) */
COPY_EXISTING ~AR1600.are~ ~override~
  LPF ~fj_are_structure~
    INT_VAR
    fj_type         = 0    //trap
    fj_box_left    = 646  //kleinstes x
    fj_box_top     = 2072   //kleinstes y
    fj_box_right   = 685  //größtes x 
    fj_box_bottom  = 2108 //größtes y
    fj_loc_x       = 655
    fj_loc_y       = 2100
    fj_vertex_0    = 646 + (2108 << 16) 
    fj_vertex_1    = 685 + (2108 << 16)
    fj_vertex_2    = 685 + (2072 << 16)
    fj_vertex_3    = 650 + (2072 << 16)
    fj_vertex_4    = 646 + (2072 << 16) 
    STR_VAR
    fj_structure_type   = region
    fj_trap_detect      = 100
    fj_trap_active      = 1
    fj_trap_status      = 0
    fj_name             = c#im1600
    fj_reg_script   = c#im1600
  END



/* Imoen abduction in Brynnlaw */
EXTEND_BOTTOM ~ar1600.bcs~ ~imoen_forever/baf/imoen_ar1600.baf~ EVALUATE_BUFFER



/* additional scripts and cutscenes */

COMPILE ~imoen_forever/baf/compile~


/* patching cutscenes */

ACTION_IF GAME_IS ~bgt~ THEN BEGIN

/* cut01bgt.bcs: exit of ID and teleport to slums if Imoen was in party in BG1 */

COPY_EXISTING ~cut01bgt.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~\("AR1512",Myself,\[1857\.1520\]\)~ 
		~"AR0406",Myself,[1321.1945]~
  END
  BUT_ONLY_IF_IT_CHANGES

END //BGT


/* Promenade fight. Cut01d.bcs - no changes */
/* cut01e.bcs: Imoen throws spells at Irenicus.- no changes */
/* cut01f.bcs - no changes */
/* cut01g.bcs - no changes */


/* Imoen gets released in Government Building - patch cutscenes */
/* movie01A, movie01B - Irenicus and Imoen are brought to the Government Building. */

COPY_EXISTING ~movie01B.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN

/* Cowled Wizards let Imoen go */
    REPLACE_TEXTUALLY ~\(DisplayStringWait("cuwizsu",56605)\)~
		~DisplayStringWait("cuwiz2",@1008)
		SmallWait(6)		
DisplayStringWait("cuimoen",@1001)
DisplayStringWait("cuimoen",@1002)
DisplayStringWait("cuwizsu",@1003)
DisplayStringWait("cuimoen",@1004)
DisplayStringWait("cujon",@1005)
		SmallWait(6)		
//DisplayStringWait("cujon",@1006)~
  END
  BUT_ONLY_IF_IT_CHANGES


/* movie02B.baf - in Spellhold. Irenicus brakes free and kills the Cowled Wizards. - no action */

/* movie02c.baf - it's not Imoen in that cell! */

COPY_EXISTING ~movie02c.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN

    REPLACE_TEXTUALLY ~\(CutSceneId("cujon2")\)~
		~CutSceneId("cujon2")
		ActionOverride("cuimoen2",SetName(@1011)
		ActionOverride("cuimoen2",Polymorph(MAGE_FEMALE_HUMAN)	
		Wait(1)~

    REPLACE_TEXTUALLY ~\(DisplayStringWait(Myself,18647)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait("cuimoen2",18648)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait(Myself,18649)\)~
		~DisplayStringWait("cuimoen2",@1009)
		SmallWait(6)
		DisplayStringWait(Myself,@1010)~
  END
  BUT_ONLY_IF_IT_CHANGES

/* movie03a.bcs (Bodhi's route) - no action */

/* movie03c.bcs (Bodhi's route) - replace "Imoen" with arbitrary thief and remove Imoen specific lines */

COPY_EXISTING ~movie03c.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~\(CutSceneId(Player1)\)~
~\1 ActionOverride("cuimoen2",Polymorph(THIEF_FEMALE_DWARF)~
    REPLACE_TEXTUALLY ~\(DisplayStringWait(Myself,56919)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait("cuimoen3",56920)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait(Myself,56921)\)~
~~
    REPLACE_TEXTUALLY ~\(DisplayStringHead(Myself,56924)[%tab% %lnl%%mnl%%wnl%]+Wait(4)\)~
~~
  END
  BUT_ONLY_IF_IT_CHANGES



/* movie03b.bcs (Aran's route) - no action */

/* movie03d.bcs (Aran's route) - replace "Imoen" with arbitrary thief and remove Imoen specific lines */

COPY_EXISTING ~movie03d.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~\(CutSceneId(Player1)\)~
~\1 ActionOverride("cuimoen3",Polymorph(THIEF_FEMALE_DWARF)~
    REPLACE_TEXTUALLY ~\(DisplayStringWait(Myself,56919)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait("cuimoen3",56920)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait(Myself,56921)\)~
~~
    REPLACE_TEXTUALLY ~\(DisplayStringHead(Myself,56924)[%tab% %lnl%%mnl%%wnl%]+Wait(4)\)~
~~
  END
  BUT_ONLY_IF_IT_CHANGES



/* compatibility: Imoen Friendship mod (first dialogue should be skipped) */

<<<<<<<< .../inlined/if_comp.baf
IF
    Global("I#ImoenFriendshipTalks","GLOBAL",1)
    Global("C#IM_ImoenStays","GLOBAL",3)
THEN
  RESPONSE #100
    SetGlobal("I#ImoenFriendshipTalks","GLOBAL",3)
END
>>>>>>>>

EXTEND_TOP ~imoen2.bcs~ ~.../inlined/if_comp.baf~ EVALUATE_BUFFER


////////////////////////////////////////////////////////////////////////
//////
////// BGII content: ~Give Imoen dialogue content in SoA~
//////
////////////////////////////////////////////////////////////////////////

BEGIN @9912 DESIGNATED 1 //GROUP @9921
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "0") @9913 
REQUIRE_PREDICATE GAME_IS ~bg2ee eet bgt tob~ @9905 
REQUIRE_PREDICATE ((GAME_IS ~bg2ee eet~) OR (MOD_IS_INSTALLED ~setup-bg2fixpack.TP2~ "0")) @9901 
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930

/* "I... er, found some things while they removed the belt. Don't look at me like that! It's not my fault they don't watch their stuff properly, is it?" */

COMPILE EVALUATE_BUFFER ~imoen_forever/dialogue/c#im_imoen2j.d~

EXTEND_BOTTOM ~imoen2.bcs~ ~imoen_forever/baf/imoen_imoen2_2.baf~ EVALUATE_BUFFER



////////////////////////////////////////////////////////////////////////
//////
////// BGII content: ~Yoshimo comes to Brynnlaw~
//////
////////////////////////////////////////////////////////////////////////

BEGIN @9920 DESIGNATED 2 //GROUP @9921
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "0") @9913 
REQUIRE_PREDICATE GAME_IS ~bg2ee eet bgt tob~ @9905 
REQUIRE_PREDICATE ((GAME_IS ~bg2ee eet~) OR (MOD_IS_INSTALLED ~setup-bg2fixpack.TP2~ "0")) @9901 
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930

/* Make Yoshimo appear at Brynnlaw - only if Imoen is in party */

/* make Yoshimo global */
EXTEND_TOP ~ar0603.bcs~ ~imoen_forever/baf/imoen_ar0603.baf~ EVALUATE_BUFFER
/* spawn Yoshimo on ship in Brynnlaw if Imoen is in party */
EXTEND_TOP ~ar1600.bcs~ ~imoen_forever/baf/imoen_ar1600_yoshimo.baf~ EVALUATE_BUFFER
/* make Yoshimo speak to the PC */
EXTEND_TOP ~yoshimo.bcs~ ~imoen_forever/baf/imoen_yoshimo.baf~ EVALUATE_BUFFER

COMPILE EVALUATE_BUFFER ~imoen_forever/dialogue/yoshimo_brynnlaw.d~ USING ~imoen_forever/tra/autotra/%LANGUAGE%/general.tra~




////////////////////////////////////////////////////////////////////////
//////
////// BGII content: ~Give Imoen protection spell in Chapters 2 & 3~
//////
////////////////////////////////////////////////////////////////////////

BEGIN @9914 DESIGNATED 3 //GROUP @9921
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "0") @9913 
REQUIRE_PREDICATE GAME_IS ~bg2ee eet bgt tob~ @9905 
REQUIRE_PREDICATE ((GAME_IS ~bg2ee eet~) OR (MOD_IS_INSTALLED ~setup-bg2fixpack.TP2~ "0")) @9901 
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930

/* Imoen will be protected against permanent death (sort of) */

ADD_SECTYPE C#IM_NoPermanentDeath ~Protection against Permanent Death~ 

/* spell protection against permanent death */
COPY ~imoen_forever/spl/c#im0001.spl~ ~override~
WRITE_BYTE 0x27 C#IM_NoPermanentDeath
WRITE_BYTE 0xa2 C#IM_NoPermanentDeath

/* remove spell protection against permanent death */
COPY ~imoen_forever/spl/c#im0002.spl~ ~override~
WRITE_BYTE 0xa2 C#IM_NoPermanentDeath

/* make Imoen apply and remove spell protection */

EXTEND_TOP ~imoen2.bcs~ ~imoen_forever/baf/imoen_imoen2.baf~ EVALUATE_BUFFER

COPY_EXISTING ~movie01B.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN

/* Cowled Wizards let Imoen go: Irenicus announces his spell on her */
    REPLACE_TEXTUALLY ~\(DisplayStringWait("cujon",@1005)[%tab% %lnl%%mnl%%wnl%]+SmallWait(6)\)~
		~\1 DisplayStringWait("cujon",@1006)~
  END
  BUT_ONLY_IF_IT_CHANGES

/* removed for compatibility reasons: Faster chapter 1&2 cutscenes and dreams TweaksAnthology
COPY_EXISTING ~movie01B.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
/* Irenicus puts a spell on Imoen */
    REPLACE_TEXTUALLY ~\(CreateVisualEffectObject("SPDISPMA","cujon")\)~
		~CreateVisualEffectObject("SPDISPMA","cujon")
		Wait(1)
		ActionOverride("cujon",ForceSpell("cuimoen",FLASHY_1))  // Imoen
		Wait(2)
		DisplayStringWait("cuwizsu",@1007)~

  END
  BUT_ONLY_IF_IT_CHANGES
*/



////////////////////////////////////////////////////////////////////////
//////
////// BGII content: ~Compatibility with Alternatives and Saerileth~
//////
////////////////////////////////////////////////////////////////////////

BEGIN @9915 DESIGNATED 9 //GROUP @9921
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "0") @9913
REQUIRE_PREDICATE GAME_IS ~bg2ee eet bgt tob~ @9905 
REQUIRE_PREDICATE ((GAME_IS ~bg2ee eet~) OR (MOD_IS_INSTALLED ~setup-bg2fixpack.TP2~ "0")) @9901  
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930

/* compatibility with Alternatives - if Alternatives is installed, change the relevant cutscenes here, too */

ACTION_IF (MOD_IS_INSTALLED ~setup-alternatives.TP2~ "0") THEN BEGIN
    PRINT @9916 /* ~Alternatives detected: patching.~ */

/* b!altm3b.baf (Aster's route) - no action */
  
/* b!altm3d.bcs (Aster's route) - replace "Imoen" with arbitrary thief and remove Imoen specific lines */

COPY_EXISTING ~b!altm3d.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~\(CutSceneId(Player1)\)~
~\1 ActionOverride("cuimoen3",Polymorph(THIEF_FEMALE_DWARF)~
    REPLACE_TEXTUALLY ~\(DisplayStringWait(Myself,56919)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait("cuimoen3",56920)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait(Myself,56921)\)~
~~
    REPLACE_TEXTUALLY ~\(DisplayStringHead(Myself,56924)[%tab% %lnl%%mnl%%wnl%]+Wait(4)\)~
~~
  END 
  BUT_ONLY_IF_IT_CHANGES



/* b!malm3b.baf (Malficus's route) - no action */

/* b!malm3d.bcs (Malficus's route) - replace "Imoen" with arbitrary thief and remove Imoen specific lines */

COPY_EXISTING ~b!malm3d.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~\(CutSceneId(Player1)\)~
~\1 ActionOverride("cuimoen2",Polymorph(THIEF_FEMALE_DWARF)~
    REPLACE_TEXTUALLY ~\(DisplayStringWait(Myself,56919)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait("cuimoen3",56920)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait(Myself,56921)\)~
~~
    REPLACE_TEXTUALLY ~\(DisplayStringHead(Myself,56924)[%tab% %lnl%%mnl%%wnl%]+Wait(4)\)~
~~
  END
  BUT_ONLY_IF_IT_CHANGES

END ELSE BEGIN
    PRINT @9917 /* ~Alternatives not detected.~ */
END




/* compatibility to Saerileth's path to Spellhold */


ACTION_IF (MOD_IS_INSTALLED ~Setup-Saerileth.TP2~ "0") THEN BEGIN
    PRINT @9918 /* ~Saerileth detected: patching.~ */

/* NSCH3a.baf - in Spellhold. cutscene start. Turn Imoen to stone */

/* NSCH3c.baf - it's not Imoen in that cell! */

COPY_EXISTING ~NSCH3c.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN

    REPLACE_TEXTUALLY ~\(CutSceneId("cujon2")\)~
		~CutSceneId("cujon2")
		ActionOverride("cuimoen2",SetName(@1011)
		ActionOverride("cuimoen2",Polymorph(MAGE_FEMALE_HUMAN)	
		Wait(1)~

    REPLACE_TEXTUALLY ~\(DisplayStringWait("cuimoen2",18648)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait(Myself,18649)\)~
		~DisplayStringWait("cuimoen2",@1009)
		SmallWait(6)
		DisplayStringWait(Myself,@1010)~
  END 
  BUT_ONLY_IF_IT_CHANGES



/* NSGOSPH2.bcs (Saerileth's route) - replace "Imoen" with arbitrary thief and remove Imoen specific lines */

COPY_EXISTING ~NSGOSPH2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
   REPLACE_TEXTUALLY ~\(CutSceneId(Player1)\)~
~\1 ActionOverride("cuimoen3",Polymorph(THIEF_FEMALE_DWARF)~
    REPLACE_TEXTUALLY ~\(DisplayStringWait(Myself,56919)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait("cuimoen3",56920)[%tab% %lnl%%mnl%%wnl%]+DisplayStringWait(Myself,56921)\)~
~~
    REPLACE_TEXTUALLY ~\(DisplayStringHead(Myself,56924)[%tab% %lnl%%mnl%%wnl%]+Wait(4)\)~
~~
  END
  BUT_ONLY_IF_IT_CHANGES

END ELSE BEGIN
    PRINT @9919 /* ~Saerileth not detected.~ */
END



////////////////////////////////////////////////////////////////////////
//////
////// SoD component: ~Imoen remains in the Group in SoD~
//////
////////////////////////////////////////////////////////////////////////


////////////////////////////////////////////////////////////////////////
/* first part: Imoen remains in the group in Korlasz' dungeon */
////////////////////////////////////////////////////////////////////////
BEGIN @9906 DESIGNATED 10 //GROUP @9922
REQUIRE_PREDICATE (GAME_INCLUDES ~sod~) @9904 
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930

/* set detecting variable for other mods - is set in Thieves Maze in BG as well as SoD Palace L3 (bd0103.bcs) 
Global("C#IM_ImoenInSoD","GLOBAL",1) */
<<<<<<<< .../i4e_sod_detection.baf
IF
	Global("C#IM_ImoenInSoD","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_ImoenInSoD","GLOBAL",1)
END
>>>>>>>>
EXTEND_BOTTOM ~%Undercity%.bcs~ ~.../i4e_sod_detection.baf~ EVALUATE_BUFFER 
EXTEND_BOTTOM ~bd0103.bcs~ ~.../i4e_sod_detection.baf~ EVALUATE_BUFFER 

/* disable all instances that make no sense for Imoen in party, but do it non-destructively. Thank you argent77 for the code! */
INCLUDE ~imoen_forever/tpa/add_trans_trigger_imoen_in_kd.tpa~

COMPILE ~imoen_forever/ImoenInGroupKD/Imoen_in_group_KD.d~ EVALUATE_BUFFER
COMPILE ~imoen_forever/ImoenInGroupKD/c#stimoe.baf~ EVALUATE_BUFFER
COMPILE ~imoen_forever/ImoenInGroupKD/c#stduja.baf~ EVALUATE_BUFFER
COMPILE ~imoen_forever/ImoenInGroupKD/c#stdu01.baf~ EVALUATE_BUFFER


EXTEND_TOP ~bdcut00z.BCS~ ~imoen_forever/ImoenInGroupKD/c#st_imo_bdcut00z.baf~ EVALUATE_BUFFER
EXTEND_TOP ~bdrope.BCS~ ~imoen_forever/ImoenInGroupKD/c#st_imo_bdrope.baf~ EVALUATE_BUFFER


/* Duke Jannath: makes sure Imoen won't stay dead in Korlasz's crypt so she can start the last dialogue */

COPY_EXISTING ~bdliia.cre~ ~override/C#STDUJA.cre~
    WRITE_ASCII 0x280 ~C#STDUJA~ #32 // set DV
    WRITE_ASCII 0x2cc ~C#STDUJA~ #8 // set dialogue file
    WRITE_ASCII 0x248 ~C#STDUJA~ #8 // set override script



//bd0120.BCS
COPY_EXISTING ~bd0120.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(InPartyAllowDead("imoen2?")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY CASE_INSENSITIVE ~%textToReplace%~ ~\1 Global("C#st_ImoenInGroupKD","GLOBAL",0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(!InMyArea("imoen2?")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 !Global("C#st_ImoenInGroupKD","GLOBAL",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[1770\.1520\]))[%newline%]*ActionOverride("imoen2?",Face(N))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#st_ImoenMoveInCrypt","GLOBAL",1)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[1545\.700\]))[%newline%]*ActionOverride("imoen2?",Face(NE))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#st_ImoenMoveInCrypt","GLOBAL",2)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY


EXTEND_TOP ~bd0120.BCS~ ~imoen_forever/ImoenInGroupKD/c#st_imo_bd0120.baf~ EVALUATE_BUFFER

<<<<<<<< .../bd0120-et.baf

IF
	InPartyAllowDead("%IMOEN_DV_SOD%")
	Global("C#st_ImoenInGroupKD","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#st_ImoenInGroupKD","GLOBAL",1)
		ApplySpellRES("bdresurr","%IMOEN_DV_SOD%")  
		SmallWait(1)
	        ApplySpellRES("bdrejuve","%IMOEN_DV_SOD%")  
	  	SmallWait(1)
END
>>>>>>>>

EXTEND_TOP ~bd0120.BCS~ ~.../bd0120-et.baf~ EVALUATE_BUFFER


//bdintro.BCS
COPY_EXISTING ~bdintro.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(CreateCreature("bdshimoe",\[881\.1652\],NE)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY


<<<<<<<< .../bdintro-et.baf

IF
	Global("SOD_fromimport","global",1)
	InPartyAllowDead("%IMOEN_DV_SOD%")  
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ActionOverride("%IMOEN_DV_SOD%",SetDialog("BDIMOEN"))
		ActionOverride("%IMOEN_DV_SOD%",ChangeAIScript("c#stimoe",OVERRIDE))
END

IF
	Global("C#st_ImoenInGroupKD","GLOBAL",0)  
THEN
	RESPONSE #100
		CutSceneId(Player1)
		CreateCreature("bdshimoe",[881.1652],NE)
END


>>>>>>>>

EXTEND_TOP ~bdintro.BCS~ ~.../bdintro-et.baf~ EVALUATE_BUFFER


//bdshcut0.BCS
COPY_EXISTING ~bdshcut0.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Global("BDSH_Spawn_Imoen","GLOBAL",0)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 Global("C#st_ImoenInGroupKD","GLOBAL",0)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY


//bd0130.BCS
COPY_EXISTING ~bd0130.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[1955\.1075\]))[%newline%]*ActionOverride("imoen2?",Face(S))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#st_ImoenMoveInCrypt","GLOBAL",3)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[3015\.1890\]))[%newline%]*ActionOverride("imoen2?",Face(SE))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#st_ImoenMoveInCrypt","GLOBAL",4)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[1830\.2390\]))[%newline%]*ActionOverride("imoen2?",Face(SE))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#st_ImoenMoveInCrypt","GLOBAL",5)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[970\.1435\]))[%newline%]*ActionOverride("imoen2?",Face(N))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#st_ImoenMoveInCrypt","GLOBAL",6)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride("imoen2?",JumpToPoint(\[1000\.1060\]))[%newline%]*ActionOverride("imoen2?",Face(N))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#st_ImoenMoveInCrypt","GLOBAL",7)~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

EXTEND_TOP ~bd0130.BCS~ ~imoen_forever/ImoenInGroupKD/c#st_imo_bd0130.baf~ EVALUATE_BUFFER


///////////////////////////////////////////////////////////////////
/* second SoD part: Imoen returns to PC in first camp bd1000 */
///////////////////////////////////////////////////////////////////
BEGIN @9907 DESIGNATED 11 //GROUP @9922
REQUIRE_PREDICATE (GAME_INCLUDES ~sod~) @9904 
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930


/* set detecting variable for other mods - is set in Thieves Maze in BG as well as SoD Palace L3 (bd0103.bcs) 
Global("C#IM_ImoenInSoD","GLOBAL",1) */
<<<<<<<< .../i4e_sod_detection_2.baf
IF
	Global("C#IM_ImoenComesBackSoD","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_ImoenComesBackSoD","GLOBAL",1)
END
>>>>>>>>
EXTEND_BOTTOM ~%Undercity%.bcs~ ~.../i4e_sod_detection_2.baf~ EVALUATE_BUFFER 
EXTEND_BOTTOM ~bd0103.bcs~ ~.../i4e_sod_detection_2.baf~ EVALUATE_BUFFER 


//  INCLUDE ~imoen_forever/lib/extra_regexp_vars.tph~


/* Adapt lines to reflect that Imoen was also in Dragonspear */
//BDIMOEN 92
/* ~I'm so sorry, <CHARNAME>. I should never have let you go to Dragonspear without me.~ */
ACTION_IF (GAME_IS ~bgee~ AND FILE_EXISTS_IN_GAME ~bd0103.are~) THEN BEGIN
  STRING_SET 39703 @5010
END
ACTION_IF (GAME_IS ~eet~) THEN BEGIN
  STRING_SET 239703 @5010
END


//add trigger point with script in bd1000 because "banter_trigger1" is unreliable for this purpose
COPY_EXISTING ~bd1000.are~ ~override~
  LPF ~fj_are_structure~
    INT_VAR
    fj_type         = 0    //proximity trigger
    fj_box_left    = 1155
    fj_box_top     = 3242
    fj_box_right   = 1271
    fj_box_bottom  = 3332
    fj_vertex_0    = 1192 + (3332 << 16)
    fj_vertex_1    = 1271 + (3329 << 16)
    fj_vertex_2    = 1257 + (3264 << 16)
    fj_vertex_3    = 1157 + (3242 << 16)
    fj_vertex_4    = 1155 + (3289 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = c#imback
    fj_reg_script   = c#imback
  END
BUT_ONLY



/* Imoen moves to war camp in bd1000.are */
EXTEND_TOP ~bd1000.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bd1000.baf~
  EVALUATE_BUFFER

/* Imoen moves to war camp in bd1000.are - teleport into area near the PC and greetings dialogue */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/sod_rejoin/c#imback.baf~ 
  
EXTEND_BOTTOM ~bdimoens.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_rejoin_bd1000.baf~
EVALUATE_BUFFER

/* and the same for the next camp in bd7100.are: */
EXTEND_TOP ~bd7100.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bd7100.baf~
  EVALUATE_BUFFER

/* Last camp is in bd3000.are. */
EXTEND_TOP ~bd3000.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bd3000.baf~
  EVALUATE_BUFFER

	/* Patch your NPC's "Move Camp" variable to the original game's script block (bd3000.bcs) */
COPY_EXISTING ~bd3000.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(SetGlobal("bd_move_npcs","bd3000",1)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 SetGlobal("C#Imoen_MoveCamp","bd3000",1)~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(SetGlobal("bd_move_voghiln","bd3000",0)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 SetGlobal("C#Imoen_MoveCamp","bd3000",0)~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY
	

/* return to camp if kicked out */
EXTEND_TOP ~bdparty.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bdparty.baf~
  EVALUATE_BUFFER

/* Imoen is kicked in hell dimension - teleport to last hell area bd4400.are */
EXTEND_TOP ~bdcut58.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bdcut58.baf~
  EVALUATE_BUFFER

/* Imoen is kicked in hell dimension or was waiting elsewhere- teleport to Dragonspear Castle interior (bd4300.are) after PC returns from Avernus */
EXTEND_TOP ~bdcut59b.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bdcut59b.baf~
  EVALUATE_BUFFER

/* after PC gets arrested for murder: Imoen leaves party */
EXTEND_TOP ~bdcut61.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bdcut61.baf~
  EVALUATE_BUFFER

/* (T3) Move NPC to entrance of the Allied Siege Camp (Crusade battle) */
EXTEND_BOTTOM ~bdasc3.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bdasc3.baf~
  EVALUATE_BUFFER



/* SoD dialogue */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/sod_rejoin/bdimoens.d~ 
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/sod_rejoin/bdimoenp.d~ 
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/sod_rejoin/bdimoen_cleanup.d~ USING ~imoen_forever/tra/autotra/%LANGUAGE%/bdimoenj.TRA~ 

/* Prevent Imoen in the Scrying Pool in bd1200.are: do it non-destructively. Thank you argent77 for the code! */
INCLUDE ~imoen_forever/tpa/add_trans_trigger_scrying_pool_cleanup.tpa~



///////////////////////////////////////////////////////////////////
/* third SoD part: Imoen gives better reason to stay behind in Palace */
///////////////////////////////////////////////////////////////////
BEGIN @9909 DESIGNATED 12 //GROUP @9922
REQUIRE_PREDICATE (GAME_INCLUDES ~sod~) @9904 
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "10") @9908
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/ImoenInGroupKD/imoen_in_palace.d~
USING ~imoen_forever/tra/autotra/%LANGUAGE%/IMOEN_IN_GROUP_KD.TRA~ 



///////////////////////////////////////////////////////////////////
/* 4th SoD part: Play cutscene with Imoen and Duke Jannath */
///////////////////////////////////////////////////////////////////
BEGIN @9910 DESIGNATED 13 //GROUP @9922
REQUIRE_PREDICATE (GAME_INCLUDES ~sod~) @9904 
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "11") @9911
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930


/* cutscene after PC could see Imoen&Liia training */
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/sod_rejoin/c#imscry.baf~ 

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/sod_rejoin/cutscene_imoen_and_jannath.d~



//////////////////////////////////////////////////////////////////
/* 5th SoD part: Give Imoen dialogue content during chapters 8 to 12 */
///////////////////////////////////////////////////////////////////
BEGIN @9923 DESIGNATED 14 //GROUP @9922
REQUIRE_PREDICATE (GAME_INCLUDES ~sod~) @9904 
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "11") @9911
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930

/* React in case crusaders in cages are killed by spikes (bd7230.are) */
//bdlever2.BCS
COPY_EXISTING ~bdlever2.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Kill("bdkharmy")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#IM_UsedSpikes","bd7230",1) \1~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY
	

EXTEND_BOTTOM ~bdimoens.bcs~ ~%MOD_FOLDER%/sod_rejoin/bdimoen_additions_sod.baf~
EVALUATE_BUFFER

/* Reaction to Caelar's first appearance on bridge */
EXTEND_BOTTOM ~bd1000.bcs~ ~%MOD_FOLDER%/sod_rejoin/bd1000_commenting.baf~
  EVALUATE_BUFFER

/* PC gave Bridgefort Castle to the crusaders */
/* & Battle at Bridgefort starts */
EXTEND_BOTTOM ~bd2000.bcs~ ~%MOD_FOLDER%/sod_rejoin/bd2000_commenting.baf~
  EVALUATE_BUFFER

/* Imoen comments on murder accusation */
EXTEND_TOP ~bd4100.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bd4100.baf~
EVALUATE_BUFFER


/* (T3) Comment after killing the Rhinoceros Beetle in bd0114.are */
EXTEND_BOTTOM ~bd0114.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bd0114.baf~
EVALUATE_BUFFER

/* (T3) Comments in bd4300.are (Bridgefort Castle interior and Avernus portal) */
EXTEND_TOP ~bd4300.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bd4300_commenting.baf~
EVALUATE_BUFFER

/* Comment upon entering Avernus */
EXTEND_BOTTOM ~bd4400.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bd4400_commenting.baf~
EVALUATE_BUFFER

/* Comment in the Avernus Elevator */
EXTEND_TOP ~bd4601.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bd4601_commenting.baf~
EVALUATE_BUFFER

/* addition to Imoen's override script */
EXTEND_BOTTOM ~bdimoens.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_rejoin_bd1000.baf~ EVALUATE_BUFFER

/* Comment in Dragonspear Castle Exterior */
EXTEND_BOTTOM ~bdbark01.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bdbark01_commenting.baf~
EVALUATE_BUFFER

/* (T3) Comment in random area (Canyon Ambush) bd0063.are */
EXTEND_BOTTOM ~bd0063.bcs~ ~%MOD_FOLDER%/sod_rejoin/imoen_bd0063.baf~
EVALUATE_BUFFER


COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/sod_rejoin/bdimoenj_additions.d~ USING ~imoen_forever/tra/autotra/%LANGUAGE%/bdimoenj.TRA~ 




//////////////////////////////////////////////////////////////////
/* 6th SoD part: Imoen at the end of the game should be the one that was in party (SoD only) */
///////////////////////////////////////////////////////////////////
/* Imoen at the end of the game should be the one that was in party (SoD only) - and talk with the right dialogue + execute the correct script */

BEGIN @9929 /* ~Imoen at the End of the Game Should be the One that Was in Party (SoD Only)~ */ DESIGNATED 15 //GROUP @9922
REQUIRE_PREDICATE ((GAME_INCLUDES ~sod~) AND GAME_IS ~bgee~) ~This component is for SoD only~
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "10") OR 
(MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "11") @9927
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930

COPY_EXISTING ~bd6200.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
        SPRINT textToReplace ~\(CreateCreature("bdimoen",\[775\.705\],E)\)~
        COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
        PATCH_IF (num_matches > 0) BEGIN
            REPLACE_TEXTUALLY ~%textToReplace%~ ~MoveGlobal("BD6200","Imoen",[775.705])  // Imoen
		ApplySpellRES("bdresurr","Imoen")  // No such index
		SmallWait(1)
		ActionOverride("Imoen",Face(E))
		ApplySpellRES("bdrejuve","Imoen")  // No such index
		ChangeEnemyAlly("Imoen",NEUTRAL)  // Imoen
		ChangeSpecifics("Imoen",ALLIES)  // Imoen
		ActionOverride("Imoen",ChangeAIScript("BDIMOEN",OVERRIDE))
		ActionOverride("Imoen",ChangeAIScript("",CLASS))
		ActionOverride("Imoen",ChangeAIScript("",RACE))
		ActionOverride("Imoen",ChangeAIScript("",GENERAL))
		ActionOverride("Imoen",ChangeAIScript("",DEFAULT))
		ActionOverride("Imoen",SetDialog("BDIMOEN"))~
            PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
        END ELSE BEGIN
            PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
        END
    END
BUT_ONLY


//////////////////////////////////////////////////////////////////
/* 7th SoD part: Unify Imoen's portrait in SoD */
///////////////////////////////////////////////////////////////////
BEGIN @9926 
SUBCOMPONENT @9924
DESIGNATED 16 //GROUP @9922
REQUIRE_PREDICATE (GAME_INCLUDES ~sod~) @9904 
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "10") OR 
(MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "11") @9927
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @9930
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @9930


  COPY ~%MOD_FOLDER%/imoen_portrait/c#imim01.spl~ ~override/c#imim01.spl~


<<<<<<<< ...inlined/imoen_portrait_sod.baf
IF
	!InParty(Myself)
	Global("C#IM_ImoenPortraitChange","MYAREA",0)
	!GlobalLT("BD_PLOT","GLOBAL",51) //After Korlasz Crypt
THEN
	RESPONSE #100
		SetGlobal("C#IM_ImoenPortraitChange","MYAREA",1)
//		ApplySpellRES("c#imim01",Myself)
		ReallyForceSpellRES("c#imim01",Myself)
		Continue()
END
>>>>>>>>

EXTEND_TOP ~bdimoens.bcs~ ~...inlined/imoen_portrait_sod.baf~

BEGIN @9925 
SUBCOMPONENT @9924
DESIGNATED 17 //GROUP @9922
REQUIRE_PREDICATE (GAME_INCLUDES ~sod~) @9904 
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "10") OR 
(MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "11") @9927


COPY_EXISTING ~bdshimoe.cre~ ~override~
  WRITE_EVALUATED_ASCII 0x34  ~IMOENM~ #8   // assign "small" portrait
  WRITE_EVALUATED_ASCII 0x3c  ~IMOENL~ #8   // assign "large" portrait

COPY_EXISTING ~bdimoen.cre~ ~override~
  WRITE_EVALUATED_ASCII 0x34  ~IMOENM~ #8   // assign "small" portrait
  WRITE_EVALUATED_ASCII 0x3c  ~IMOENL~ #8   // assign "large" portrait


//////////////////////////////////////////////////////////////////
/* 8th SoD part: Imoen's chest moves with the campaign, too */
///////////////////////////////////////////////////////////////////
BEGIN @9931 DESIGNATED 20 //GROUP @9922
REQUIRE_PREDICATE (GAME_INCLUDES ~sod~) @9904 
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~setup-imoen_forever.TP2~ "11") @9911

/* Imoen'll just stuff her stuff into the tent next to CHARNAME's chest. */
COPY_EXISTING ~bd1000.are~ ~override~
//container and item
  LPF ~fj_are_structure~
    INT_VAR
    fj_type        = 8 //non-visible
    fj_loc_x       = 116
    fj_loc_y       = 3643
    fj_box_left    = 65
    fj_box_top     = 3596
    fj_box_right   = 91
    fj_box_bottom  = 3634
    fj_trap_loc_x  = 116
    fj_trap_loc_y  = 3643
    fj_vertex_0    = 65 + (3633 << 16)
    fj_vertex_1    = 91 + (3634 << 16)
    fj_vertex_2    = 91 + (3609 << 16)
    fj_vertex_3    = 72 + (3596 << 16)
    STR_VAR
    fj_structure_type = container
    fj_name           = ~C#ImoensChest~
  END
BUT_ONLY

COPY_EXISTING ~bd3000.are~ ~override~
//container and item
  LPF ~fj_are_structure~
    INT_VAR
    fj_type        = 8 //non-visible
    fj_loc_x       = 1579
    fj_loc_y       = 1871
    fj_box_left    = 1588
    fj_box_top     = 1799
    fj_box_right   = 1618
    fj_box_bottom  = 1861
    fj_trap_loc_x  = 1579
    fj_trap_loc_y  = 1871
    fj_vertex_0    = 1588 + (1840 << 16)
    fj_vertex_1    = 1614 + (1861 << 16)
    fj_vertex_2    = 1618 + (1822 << 16)
    fj_vertex_3    = 1613 + (1799 << 16)
    STR_VAR
    fj_structure_type = container
    fj_name           = ~C#ImoensChest~
  END
BUT_ONLY

COPY_EXISTING ~bd7100.are~ ~override~
//container and item
  LPF ~fj_are_structure~
    INT_VAR
    fj_type        = 8 //non-visible
    fj_loc_x       = 243
    fj_loc_y       = 3457
    fj_box_left    = 208
    fj_box_top     = 3401
    fj_box_right   = 223
    fj_box_bottom  = 3453
    fj_trap_loc_x  = 1579
    fj_trap_loc_y  = 1871
    fj_vertex_0    = 208 + (3453 << 16)
    fj_vertex_1    = 223 + (3440 << 16)
    fj_vertex_2    = 221 + (3421 << 16)
    fj_vertex_3    = 208 + (3401 << 16)
    STR_VAR
    fj_structure_type = container
    fj_name           = ~C#ImoensChest~
  END
BUT_ONLY

<<<<<<<< .../move_imoens_stuff_bd1000.baf
IF
	Global("C#I4E_bdimoen_chest","GLOBAL",1)
	!Global("C#I4E_MoveImoensStuff","GLOBAL",1)
	Global("C#I4E_MoveImoensStuff","GLOBAL",0)
THEN
	RESPONSE #100
		MoveContainerContents("BD0103*Imoen_equipment","BD1000*C#ImoensChest")
		SetGlobal("C#I4E_MoveImoensStuff","GLOBAL",1)
END
>>>>>>>>
EXTEND_BOTTOM ~bd1000.bcs~ ~.../move_imoens_stuff_bd1000.baf~ EVALUATE_BUFFER

<<<<<<<< .../move_imoens_stuff_bd3000.baf
IF
	Global("C#I4E_bdimoen_chest","GLOBAL",1)
	!Global("C#I4E_MoveImoensStuff","GLOBAL",3)
	Global("C#I4E_MoveImoensStuff","GLOBAL",0)
THEN
	RESPONSE #100
		MoveContainerContents("BD0103*Imoen_equipment","BD3000*C#ImoensChest")
		SetGlobal("C#I4E_MoveImoensStuff","GLOBAL",3)
END

IF
	Global("C#I4E_bdimoen_chest","GLOBAL",1)
	!Global("C#I4E_MoveImoensStuff","GLOBAL",3)
	Global("C#I4E_MoveImoensStuff","GLOBAL",1)
THEN
	RESPONSE #100
		MoveContainerContents("BD1000*C#ImoensChest","BD3000*C#ImoensChest")
		SetGlobal("C#I4E_MoveImoensStuff","GLOBAL",3)
END

IF
	Global("C#I4E_bdimoen_chest","GLOBAL",1)
	!Global("C#I4E_MoveImoensStuff","GLOBAL",3)
	Global("C#I4E_MoveImoensStuff","GLOBAL",2)
THEN
	RESPONSE #100
		MoveContainerContents("BD7100*C#ImoensChest","BD3000*C#ImoensChest")
		SetGlobal("C#I4E_MoveImoensStuff","GLOBAL",3)
		Continue()
END
>>>>>>>>
EXTEND_BOTTOM ~bd3000.bcs~ ~.../move_imoens_stuff_bd3000.baf~ EVALUATE_BUFFER

<<<<<<<< .../move_imoens_stuff_bd7100.baf
IF
	Global("C#I4E_bdimoen_chest","GLOBAL",1)
	!Global("C#I4E_MoveImoensStuff","GLOBAL",2)
	Global("C#I4E_MoveImoensStuff","GLOBAL",0)
THEN
	RESPONSE #100
		MoveContainerContents("BD0103*Imoen_equipment","BD7100*C#ImoensChest")
		SetGlobal("C#I4E_MoveImoensStuff","GLOBAL",2)
END

IF
	Global("C#I4E_bdimoen_chest","GLOBAL",1)
	!Global("C#I4E_MoveImoensStuff","GLOBAL",2)
	Global("C#I4E_MoveImoensStuff","GLOBAL",1)
THEN
	RESPONSE #100
		MoveContainerContents("BD1000*C#ImoensChest","BD7100*C#ImoensChest")
		SetGlobal("C#I4E_MoveImoensStuff","GLOBAL",2)
END
>>>>>>>>
EXTEND_BOTTOM ~bd7100.bcs~ ~.../move_imoens_stuff_bd7100.baf~ EVALUATE_BUFFER

// Get state for bdimoens %heya_its_me_04% (in bdimoens.d)
/* ~Oh, yes! This is gonna be so great!~
*/
OUTER_SET heya_its_me_04 = STATE_WHICH_SAYS 18 IN ~imoen_forever/tra/autotra/%s/bdimoens.tra~ FROM ~bdimoens~

<<<<<<<< .../add_bdimoen_chest.d
I_C_T bdimoens %heya_its_me_04% C#I4E_bdimoen_chest
== bdimoens @24
DO ~AddJournalEntry(@25,INFO)~
END
>>>>>>>>
COMPILE EVALUATE_BUFFER ~.../add_bdimoen_chest.d~ USING ~imoen_forever/tra/autotra/%s/bdimoens.tra~


