/* set dialogue timer after joining in the slums */
IF
	InParty(Myself)
	Global("C#IM_ImoenStays","GLOBAL",1)
	Global("C#IM_DialogueTimerSet","GLOBAL",0)
THEN  
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",800)
		RealSetGlobalTimer("C#IM_IrenicusTimer","GLOBAL",14400) // three hours
		SetGlobal("C#IM_DialogueSoA","GLOBAL",1)
		SetGlobal("C#IM_DialogueTimerSet","GLOBAL",1)
END

/* set dialogue timer after joining in Spellhold */
IF
	InParty(Myself)
	GlobalGT("Chapter","GLOBAL",3) 
	!Global("C#IM_ImoenStays","GLOBAL",1)
	GlobalLT("C#IM_DialogueTimerSet","GLOBAL",2)
THEN  
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",700)
		SetGlobal("C#IM_DialogueSoA","GLOBAL",51)
		SetGlobal("C#IM_DialogueTimerSet","GLOBAL",2)
END



/* "Look what I found!" */
IF
	Global("C#IM_ImoenStays","GLOBAL",1)
	Global("C#IM_FoundThings","LOCALS",0)
THEN
	RESPONSE #100
		CreateItem("MISC41",1,0,0)
		CreateItem("MISC17",2,0,0)
		CreateItem("MISC18",1,0,0)
		CreateItem("POTN52",1,0,0)
		CreateItem("SCRL05",1,0,0)
		CreateItem("SCRL1D",1,0,0)
		CreateItem("SCRL6F",1,0,0)
		SetGlobal("C#IM_FoundThings","LOCALS",1) 
END

IF
	InParty(Myself)
	Detect(Player1)
	!See([ENEMY])
	CombatCounter(0)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_FoundThings","LOCALS",1)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* 1 st friendship dialogue chapter 2 */
/* Is Irenicus still a threat? */
/* activate */
IF 
    RealGlobalTimerExpired("C#IM_DialogueTimer","GLOBAL")
    Global("C#IM_DialogueSoA","GLOBAL",1)
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_DialogueSoA","GLOBAL",2)
END

/* initiate */
IF 
  Global("C#IM_DialogueSoA","GLOBAL",2) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END

/* player dawdles in Chapter 2 */
/* activate */
IF 
    RealGlobalTimerExpired("C#IM_IrenicusTimer","GLOBAL")
    Global("C#IM_LookForIrenicus","GLOBAL",0)
    Global("Chapter","GLOBAL",%bg2_chapter_2%)
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_LookForIrenicus","GLOBAL",1)
END

/* initiate */
IF 
  Global("C#IM_LookForIrenicus","GLOBAL",1) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END



/* Player bought passage to Brynnlaw: dialogue activation */

IF 
	InParty(Myself)
        See(Player1)
        !Detect([ENEMY])
        !Detect([EVILBUTBLUE])
        CombatCounter(0)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("Chapter","GLOBAL",%bg2_chapter_3%)
	Global("C#IM_BoughtPassage","GLOBAL",0) 
	OR(3)
	  Global("WorkingForAran","GLOBAL",1) //bought passage by the Shadow Thieves
	  Global("WorkingForBodhi","GLOBAL",1) //bought passage by Bodhi
	  GlobalGT("B!Alternatives","GLOBAL",5) //bought passage by Aster (Alternatives Mod)
	!See("Gaelan")
	!See("bodhi")
	!See("b!aster")
THEN
	RESPONSE #100
		SetGlobal("C#IM_BoughtPassage","GLOBAL",1)
END

/* Player bought passage to Brynnlaw: dialogue triggering */

IF 
	InParty(Myself)
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_BoughtPassage","GLOBAL",1)

THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END




/* after fight against Bodhi for Shadow Thieves: Bodhi hinted at Imoen being special */
/* activate */
IF 
    Global("C#IM_BODHI2_14","GLOBAL",1) //interjection run
    !AreaCheck("AR0801") //dont talk directly about it
    !AreaCheck("AR0803") 
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_BODHI2_14","GLOBAL",2)
END

/* initiate */
IF 
  Global("C#IM_BODHI2_14","GLOBAL",2) 
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
THEN
	RESPONSE #100
		StartDialogueNoSet(Player1)
END


/* First dream.

This block makes sure the dream talk doesn't fire at a later state, if Imoen wasn't in the party */

IF
	!InParty(Myself)
	Global("HadImoenDream1","GLOBAL",1)
	Global("C#IM_HadDream1","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_HadDream1","GLOBAL",3)
		Continue()
END


/* Second dream 
This block makes sure the dream talk doesn't fire at a later state, if Imoen wasn't in the party */
IF
	!InParty(Myself)
	Global("HadJonDream1","GLOBAL",1)
	Global("C#IM_HadDream2","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_HadDream2","GLOBAL",3) 
		Continue()
END


/* Third dream. 
This block makes sure the dream talk doesn't fire at a later state, if Imoen wasn't in the party */

IF
	!InParty(Myself)
	Global("HadJonDream2","GLOBAL",1)
	Global("C#IM_HadDream3","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#IM_HadDream3","GLOBAL",3) 
		Continue()
END



/* First dream: dialogue activation */

IF 
	InParty(Myself)
        See(Player1)
        !Detect([ENEMY])
        !Detect([EVILBUTBLUE])
        CombatCounter(0)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_HadDream1","GLOBAL",0) 
	Global("HadImoenDream1","GLOBAL",1)

THEN
	RESPONSE #100
		SetGlobal("C#IM_HadDream1","GLOBAL",1)
END

/* First dream: dialogue triggering */

IF 
	InParty(Myself)
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_HadDream1","GLOBAL",1)

THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END



/* Second dream: dialogue activation */
IF 
	InParty(Myself)
        See(Player1)
        !Detect([ENEMY])
        !Detect([EVILBUTBLUE])
        CombatCounter(0)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_HadDream2","GLOBAL",0) 
	Global("HadJonDream1","GLOBAL",1)

THEN
	RESPONSE #100
		SetGlobal("C#IM_HadDream2","GLOBAL",1) 
END

/* Second dream: dialogue triggering */
IF 
	InParty(Myself)
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_HadDream2","GLOBAL",1) 

THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END

/* Third dream: dialogue activation */

IF 
	InParty(Myself)
        See(Player1)
        !Detect([ENEMY])
        !Detect([EVILBUTBLUE])
        CombatCounter(0)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_HadDream3","GLOBAL",0) 
	Global("HadJonDream2","GLOBAL",1)

THEN
	RESPONSE #100
		SetGlobal("C#IM_HadDream3","GLOBAL",1) 
END

/* Third dream: dialogue triggering */

IF 
	InParty(Myself)
	Detect(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#IM_HadDream3","GLOBAL",1)

THEN
	RESPONSE #100
		PlaySong(0)
		PlaySound("C#AJROM")
		StartDialogNoSet(Player1)
END

/* After Spellhold */
/* activate */
IF 
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
    RealGlobalTimerExpired("C#IM_DialogueTimer","GLOBAL")
    OR(4)
	Global("C#IM_DialogueSoA","GLOBAL",51)
	Global("C#IM_DialogueSoA","GLOBAL",53)
	Global("C#IM_DialogueSoA","GLOBAL",55)
	Global("C#IM_DialogueSoA","GLOBAL",57)
THEN
	RESPONSE #100
		IncrementGlobal("C#IM_DialogueSoA","GLOBAL",1)
END

/* initiate */
IF 
    InParty(Myself)
  !StateCheck(Player1,CD_STATE_NOTVALID)
  !StateCheck(Myself,CD_STATE_NOTVALID)
    See(Player1)
    !Detect([ENEMY])
    !Detect([EVILBUTBLUE])
    CombatCounter(0)
    OR(4)
	Global("C#IM_DialogueSoA","GLOBAL",52)
	Global("C#IM_DialogueSoA","GLOBAL",54)
	Global("C#IM_DialogueSoA","GLOBAL",56)
	Global("C#IM_DialogueSoA","GLOBAL",58)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#IM_DialogueTimer","GLOBAL",1200)
		StartDialogueNoSet(Player1)
END